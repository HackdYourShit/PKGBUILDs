# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix FTBFS on ARM: https://git.gnome.org/browse/gnome-commander/commit/?id=4a903688d99ea96cc026b3c0939399849ae9e2b4
#  - can likely be removed on version bump

pkgname=gnome-commander
pkgver=1.4.0
pkgrel=1
pkgdesc='Graphical two-pane filemanager for Gnome'
arch=('i686' 'x86_64')
url='http://gcmd.github.io/'
license=('GPL')
depends=('libgnomeui' 'gnome-vfs' 'gconf' 'python2' 'libsm' 'libunique')
makedepends=('perl-xml-parser' 'gnome-doc-utils' 'intltool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(ftp://ftp.gnome.org/pub/GNOME/sources/gnome-commander/1.4/$pkgname-$pkgver.tar.xz
        vtable.patch)
sha256sums=('ffd1062dd61ca993271e6d013dccf179fd8291f3507907c873facdf893ef6fc1'
            '43c19f8b0e70bf77fc8558d0f48caa4fe981f14d2d9ca440f0222e3c5a69cbd4')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../vtable.patch
}

build() {
  cd "$pkgname-$pkgver"

  # Python 2 fix
  for f in doc/*/gnome-commander.xml; do
      sed -i 's:env python:env python2:' "$f"
  done

  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper --enable-python
  make
}

package() {
  cd "$pkgname-$pkgver"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" \
    --domain "$pkgname" "$pkgdir/etc/gconf/schemas/"*.schemas
  rm -rf "$pkgdir/etc/gconf/schemas/"
}

# vim:set ts=2 sw=2 et:
