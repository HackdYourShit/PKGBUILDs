# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with musl-gcc

pkgname=busybox
pkgver=1.25.1
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="http://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
install=busybox.install
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"
	"config")
md5sums=('4f4c5de50b479b11ff636d7d8eb902a2'
         '1b578c4a2c1cfc2376da9243675cde89')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp $srcdir/config .config
  sed '1,1i#include <sys/resource.h>' -i include/libbb.h
  # if you want to run menuconfig uncomment the following line:
#  make menuconfig ; return 1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 busybox $pkgdir/usr/bin/busybox
  install -Dm644 docs/busybox.1 $pkgdir/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc $pkgdir/usr/share/doc/$pkgname/$doc
  done
}
