# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: <boenki@gmx.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora to fix signed char usage

pkgname=qlandkartegt
pkgver=1.8.1
pkgrel=7
pkgdesc="Use your GPS with Linux"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org/"
license=('GPL')
depends=('gdal' 'gpsd' 'libdmtx' 'libexif' 'libmariadbclient' 'glu'
         'postgresql-libs' 'shared-mime-info' 'qtwebkit' 'proj')
makedepends=('cmake' 'garmindev' 'mesa')
optdepends=('garmindev: to connect garmin devices')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname-mimetypes.xml
        qlandkartegt-1.8.1-char.patch)
sha256sums=('9e0605ab8f4cbd27741b507f252f08370e9ccca4e05ec35878256c59b22a9be7'
            'e8d33948831f8a66edd752c71c653085d6c4d9f1969e70dd8c40b9c7ca37fba0'
            '60388727cc8416bddff05af1251661db4e6b3022436dcf80b4d8251f0437900a')

prepare() {
  cd "${srcdir}"
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i ../qlandkartegt-1.8.1-char.patch
}

build() {
  cd "${srcdir}"/build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package(){
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/$pkgname-mimetypes.xml \
    "${pkgdir}"/usr/share/mime/packages/$pkgname-mimetypes.xml
}
