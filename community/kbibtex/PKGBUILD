# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to remove qreal usage on v5/6/7

pkgname=kbibtex
pkgver=0.6
_branch=0.6
pkgrel=5
pkgdesc="A BibTeX editor for KDE"
arch=('i686' 'x86_64')
url='http://home.gna.org/kbibtex/'
license=('GPL')
depends=('kdebase-runtime' 'poppler-qt4')
optdepends=('okular: Document preview')
makedepends=('cmake' 'automoc4')
source=("http://download.gna.org/$pkgname/$_branch/$pkgname-$pkgver.tar.xz" kbibtex-no-qtwebkit.patch
        '0001-remove-qreal-cast-for-arm.patch')
md5sums=('f1a9c1867d94c36fe88cfa0470b85dac'
         '73a6be6e48e0e9106f60b65ad07d73e6'
         'fae1a894b4dbc9339b0ad70a3a3b185c')

prepare() {
  cd "$pkgname-$pkgver"

# Disable insecure qtwebkit
  patch -p1 -i ../kbibtex-no-qtwebkit.patch

  if [[ $CARCH != "aarch64" ]]; then
    patch -p1 -i ../0001-remove-qreal-cast-for-arm.patch
  fi
}

build() {
  cd "$pkgname-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
