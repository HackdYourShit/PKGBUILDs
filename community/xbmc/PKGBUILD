# $Id: PKGBUILD 71695 2012-06-01 13:05:36Z dreisner $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - --disable-vdpau/xvda, remove from makedepends
#  - --disable-vaapi
#  - --enable-gl -> --enable-gles
#  - specify our C/CXXFLAGS in configure

buildarch=4

_prefix=/usr

pkgname=xbmc
pkgver=13.0
_codename=Gotham
pkgrel=2
pkgdesc="A software media player and entertainment hub for digital media"
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL' 'custom')
depends=('hicolor-icon-theme' 'fribidi' 'lzo2' 'smbclient' 'libtiff' 'libva'
         'libpng' 'libcdio' 'yajl' 'libmysqlclient' 'libjpeg-turbo' 'libsamplerate'
         'glew' 'libssh' 'libmicrohttpd' 'libxrandr' 'sdl_mixer' 'sdl_image'
         'python2' 'libass' 'libmpeg2' 'libmad' 'libmodplug' 'jasper' 'rtmpdump'
         'unzip' 'mesa-demos' 'xorg-xdpyinfo' 'libbluray' 'libnfs' 'afpfs-ng'
         'avahi' 'bluez-libs' 'tinyxml' 'libcap' 'swig' 'taglib' 'libpulse'
         'java-runtime-headless' 'glu' 'mesa' 'shairplay' 'libxslt' 'ffmpeg')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libxinerama' 'zip' 'libcec'
	     'udisks' 'upower' 'mesa' 'doxygen' 'swig' 'java-environment')
optdepends=('libcec: support for Pulse-Eight USB-CEC adapter'
	    'lirc: remote controller support'
	    'udisks: automount external drives'
	    'upower: used to trigger power management functionality'
	    'unrar: access compressed files without unpacking them'
	    'xorg-xinit: autostart xbmc'
	    'pulseaudio: for pulseaudio support')
install="${pkgname}.install"
source=("xbmc-$pkgver.tar.gz::https://github.com/xbmc/xbmc/archive/13.0-Gotham.tar.gz"
	"xbmc.service"
	"polkit.rules"
	"enable-external-ffmpeg.patch")
md5sums=('54fafd985b5209859ba6faeb50b8f142'
         'de331663cc8adc94f2fe44a262cfff58'
         '02f7951824ee13103344f36009c0ef2a'
         '9f9b41cfc1e21b1e7d0bb10d6e85b3c6')

prepare() {
  cd "${srcdir}/xbmc-$pkgver-$_codename"

  patch -p1 -i ../enable-external-ffmpeg.patch
}

build() {
  cd "${srcdir}/xbmc-$pkgver-$_codename"

  # Bootstrapping XBMC
  ./bootstrap

  # Configuring XBMC
  export PYTHON_VERSION=2  # external python v2
  ./configure --prefix=$_prefix --exec-prefix=$_prefix \
    --disable-debug \
    --enable-optimizations \
    --enable-gles \
    --enable-sdl \
    --disable-vaapi \
    --disable-vdpau \
    --disable-xvba \
    --enable-joystick \
    --enable-xrandr \
    --enable-rsxs \
    --enable-projectm \
    --enable-x11 \
    --enable-rtmp \
    --enable-samba \
    --enable-nfs \
    --enable-afpclient \
    --enable-airplay \
    --enable-airtunes \
    --enable-ffmpeg-libvorbis \
    --enable-dvdcss \
    --disable-hal \
    --enable-avahi \
    --enable-webserver \
    --enable-optical-drive \
    --enable-libbluray \
    --enable-texturepacker \
    --enable-udev \
    --enable-libusb \
    --enable-libcec \
    --enable-external-libraries \
    CFLAGS="${CFLAGS}" CXXFLAGS="${CXXFLAGS}"
  # Now (finally) build
  make
}

package() {
  cd "${srcdir}/xbmc-$pkgver-$_codename"
  # Running make install
  make DESTDIR="${pkgdir}" install

  # run feh with python2
  sed -i -e 's/python/python2/g' ${pkgdir}${_prefix}/bin/xbmc

  # Tools
  #install -D -m 0755 "${srcdir}/xbmc-$pkgver-$_codename/xbmc-xrandr" "${pkgdir}${_prefix}/share/xbmc/xbmc-xrandr"
  # Stupid TexturePacker does not exist.
  #install -D -m 0755 "${srcdir}/xbmc-$pkgver-$_codename/tools/TexturePacker/TexturePacker" "${pkgdir}${_prefix}/share/xbmc/"

  # Licenses
  install -d -m 0755 "${pkgdir}${_prefix}/share/licenses/${pkgname}"
  for licensef in LICENSE.GPL copying.txt; do
    mv "${pkgdir}${_prefix}/share/doc/xbmc/${licensef}" "${pkgdir}${_prefix}/share/licenses/${pkgname}"
  done

  install -Dm0644 $srcdir/xbmc.service $pkgdir/usr/lib/systemd/system/xbmc.service
  install -Dm0644 $srcdir/polkit.rules $pkgdir/etc/polkit-1/rules.d/10-xbmc.rules
}
