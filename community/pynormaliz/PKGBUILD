# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix AArch64 build

pkgbase=pynormaliz
pkgname=(python2-pynormaliz python-pynormaliz)
pkgver=1.1
pkgrel=2
pkgdesc="A Python interface to Normaliz"
arch=(i686 x86_64)
url="https://github.com/sebasguts/PyNormaliz"
license=(GPL2)
makedepends=(python-setuptools python2-setuptools normaliz)
source=("https://pypi.io/packages/source/P/PyNormaliz/PyNormaliz-$pkgver.tar.gz"
        '0001-AArch64-fix.patch')
md5sums=('d36a2b6b53a095b5c09544a5d0662178'
         '456c661216d76a0f819650f79974626b')

prepare() {
  patch -p1 -d PyNormaliz-$pkgver -i "${srcdir}/0001-AArch64-fix.patch"
  cp -r PyNormaliz-$pkgver PyNormaliz-$pkgver-py2
}

build() {
  cd PyNormaliz-$pkgver
  python setup.py build

  cd ../PyNormaliz-$pkgver-py2
  python2 setup.py build
}

package_python2-pynormaliz() {
  depends=(python2 normaliz)
  cd PyNormaliz-$pkgver-py2

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python-pynormaliz() {
  depends=(python normaliz)
  cd PyNormaliz-$pkgver
  
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

