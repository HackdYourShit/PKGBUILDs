# Test programs for IMX BSP
# Maintainer: RaumZeit <raumzeit@gmx.net>

buildarch=4

pkgname=imx-test
pkgver=3.10.9_1.0.0
_pkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="Unit tests for Freescale i.MX6Q SoC - Platform specific code for Freescale imx platform"

# filesystem prefix for the include files and license
_install_prefix=/opt/imx

url="https://community.freescale.com/"
arch=('armv7h')
license=('GPLv2')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${_pkgver}.tar.gz")
md5sums=('518ce3fba668a09993d53b3b0df85944')
makedepends=('asciidoc')
depends=('imx-lib' 'imx-vpu')

build() {
  cd "${pkgname}-${_pkgver}"
  export PLATFORM="IMX6Q"
  export KERNEL_SRC="/usr/src/linux-3.0.35_4.1.0-6-ARCH+/"  
  export INCLUDE_DIR="-I./include \
                  -I${KERNEL_SRC}/include/uapi \
                  -I${KERNEL_SRC}/include/ \
                  -I${KERNEL_SRC}/arch/arm/include \
                  -I${KERNEL_SRC}/drivers/mxc/security/rng/include \
                  -I${KERNEL_SRC}/drivers/mxc/security/sahara2/include"
  LDFLAGS="" make LINUXPATH=${KERNEL_SRC} \
  KBUILD_OUTPUT=${KERNEL_SRC} V=1 \
  INC="${INCLUDE_DIR}"
}

package() {
  cd "${pkgname}-${_pkgver}"
  mkdir -p "${pkgdir}/${_install_prefix}/unit_tests"

  install -m 755 test-utils.sh "${pkgdir}/${_install_prefix}/unit_tests/test-utils.sh"
  install -m 755 "${srcdir}"/platform/IMX6Q/* "${pkgdir}/${_install_prefix}/unit_tests/"
  if [ -e ${WORKDIR}/clocks.sh ]; then
    install -m 755 ${WORKDIR}/clocks.sh "${pkgdir}/${_install_prefix}/unit_tests/clocks.sh"
  fi

}

# vim:set ts=2 sw=2 et:
