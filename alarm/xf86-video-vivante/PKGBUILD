# Xorg driver for VIVANTE GPU
# Maintainer: RaumZeit <raumzeit@gmx.net>

buildarch=4

pkgname=xf86-video-vivante
_yocto_pkgname=xserver-xorg-video-imx-viv
pkgver=3.5.7_1.0.0
_pkgver=${pkgver/_/-}-alpha.2
pkgrel=1
pkgdesc="Xorg driver for proprietary VIVANTE GPU drivers"

url="https://community.freescale.com/"
arch=('armv7h')
license=('MIT')
depends=('libgal-x11' 'libx11' 'libdrm-viv' 'gpu-viv-bin-mx6q-x11=4.1.0')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${_yocto_pkgname}-${_pkgver}.tar.gz"
        'libdrm.patch')
md5sums=( '3fc3d355d721656144920afd06fb1bff'
          'SKIP')


prepare() {
  cd "${_yocto_pkgname}-${_pkgver}"
  patch -p1 < "${srcdir}"/libdrm.patch
}

build() {
  cd "${_yocto_pkgname}-${_pkgver}"
  make -C EXA/src -f makefile.linux \
  BUSID_HAS_NUMBER=1 \
  XSERVER_GREATER_THAN_13=1 \
  BUILD_HARD_VFP=1 \
  CFLAGS+=-I../../DRI_1.10.4/src
}

package() {
  cd "${_yocto_pkgname}-${_pkgver}"
  mkdir -p "${pkgdir}/usr/lib/xorg/modules/drivers"
  mkdir -p "${pkgdir}/usr/include"
  cp -axr EXA/src/vivante_drv.so "${pkgdir}/usr/lib/xorg/modules/drivers/"
  cp -axr EXA/src/vivante_gal/vivante_priv.h "${pkgdir}/usr/include/"
  cp -axr EXA/src/vivante_gal/vivante_gal.h "${pkgdir}/usr/include/"
  find "${pkgdir}/usr/include/" -type f -exec chmod 660 {} \;
}

# vim:set ts=2 sw=2 et:
