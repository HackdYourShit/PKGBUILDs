# Xorg driver for VIVANTE GPU
# Maintainer: RaumZeit <raumzeit@gmx.net>

buildarch=4

pkgname=xf86-video-vivante
_yocto_pkgname=xserver-xorg-video-imx-viv
pkgver=3.10.17_1.0.0
_pkgver=${pkgver/_/-}_beta
pkgrel=1
pkgdesc="Xorg driver for proprietary VIVANTE GPU drivers"

url="https://community.freescale.com/"
arch=('armv7h')
license=('MIT')
makedepends=('xorg-server-devel')
depends=('libdrm-viv' 'libx11' 'gpu-viv-bin-mx6q-x11=3.10.17_1.0.0_beta')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${_yocto_pkgname}-${_pkgver}.tar.gz"
        'libdrm.patch'
        'fix-hdmi-preferred-mode.patch')
md5sums=( 'fee0e4820fe3f89e5f36a0b343e4a33f'
          '2f1ba8786bfc5fbc482fb0fbad6362ff'
          '9ca87352aecc6b309aa5f5b2bb9d6e24')


prepare() {
  cd "${_yocto_pkgname}-${_pkgver}"
  patch -p1 < "${srcdir}"/libdrm.patch
  patch -p1 < "${srcdir}"/fix-hdmi-preferred-mode.patch
}

build() {
  cd "${_yocto_pkgname}-${_pkgver}"
  make -C EXA/src -f makefile.linux \
  BUSID_HAS_NUMBER=1 \
  XSERVER_GREATER_THAN_13=1 \
  BUILD_HARD_VFP=1 \
  CFLAGS+=-I../../DRI_1.10.4/src
}

package() {
  cd "${_yocto_pkgname}-${_pkgver}"
  mkdir -p "${pkgdir}/usr/lib/xorg/modules/drivers"
  mkdir -p "${pkgdir}/usr/include"
  cp -axr EXA/src/vivante_drv.so "${pkgdir}/usr/lib/xorg/modules/drivers/"
  cp -axr EXA/src/vivante_gal/vivante_priv.h "${pkgdir}/usr/include/"
  cp -axr EXA/src/vivante_gal/vivante_gal.h "${pkgdir}/usr/include/"
  find "${pkgdir}/usr/include/" -type f -exec chmod 660 {} \;
}

# vim:set ts=2 sw=2 et:
