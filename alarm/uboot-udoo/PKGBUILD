# U-Boot: UDOO
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-udoo
pkgver=2016.09
pkgrel=1
pkgdesc="U-Boot for UDOO Dual and Quad"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
makedepends=('bc' 'dtc' 'git')
replaces=('uboot-udoo-dual' 'uboot-udoo-quad')
license=('GPL')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch')
md5sums=('2e69dda70eb28f8042d2f9fbeb1feaa1'
         '6165211bb53ee947ce58a22828984b5e')

prepare() {
  cd u-boot-${pkgver/rc/-rc}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver/rc/-rc}
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  make distclean
  make udoo_defconfig
  make EXTRAVERSION=-${pkgrel}
}

package_uboot-udoo() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}"/boot
  cp SPL u-boot.img "${pkgdir}"/boot
}
