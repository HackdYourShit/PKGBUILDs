# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable omap, exynos, tegra, etnaviv for v7
#  - drop valgrind makdepend for v5 and v6
#  - upstream patches to fix glamoregl segfault with Mesa 13.0

pkgname=libdrm
pkgver=2.4.71
pkgrel=1.2
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('xorg-util-macros' 'libxslt' 'docbook-xsl')
makedepends_armv7h=('valgrind')
makedepends_aarch64=('valgrind')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig}
        0001-Return-an-ENODEV-from-drmGetDevice-when-no-device-wa.patch
        0002-Silence-runtime-complaints-on-platform-devices.patch
        COPYING)
sha256sums=('c66287ddeee5f46ea8f8880b94b80acb3bbc33ba6321d17767eef145046df9b8'
            'SKIP'
            '2b35620c67771ef286106863076f7f544d7795d53368f51b757105865d82b88f'
            'bc6ef82d3a4a601590ea40dc644734cfc05aa6e74dbdb3401d98e966d7f45e48'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')
validpgpkeys=('B97BD6A80CAC4981091AE547FE558C72A67013C3') # Maarten Lankhorst <maarten.lankhorst@canonical.com>
validpgpkeys+=('215DEE688925CCB965BE5DA97C03D7797B6E1AE2') # Damien Lespiau <damien.lespiau@intel.com>
validpgpkeys+=('10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE') # David Airlie <airlied@redhat.com>
validpgpkeys+=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>
validpgpkeys+=('D6285B5E899299F3DA746184191C9B905522B045') # Rob Clark <robclark@freedesktop.org>
validpgpkeys+=('E8EB5B34081CE1EEA26EFE195B5BDA071D49CC38') # Kenneth Graunke <kenneth.w.graunke@intel.com>
validpgpkeys+=('FC9BAE1435A9F7F664B82057B5D62936D1FC9EE8') # Eric Anholt <eric@anholt.net>

prepare() {
  cd $pkgname-$pkgver
  
  patch -p1 -i ../0001-Return-an-ENODEV-from-drmGetDevice-when-no-device-wa.patch
  patch -p1 -i ../0002-Silence-runtime-complaints-on-platform-devices.patch
  
  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install

}

build() {
  cd $pkgname-$pkgver
  [[ "$CARCH" == "armv7h" ]] && CONFIG="--enable-omap-experimental-api --enable-exynos-experimental-api --enable-tegra-experimental-api --enable-etnaviv-experimental-api"
  ./configure --prefix=/usr --enable-udev $CONFIG
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
