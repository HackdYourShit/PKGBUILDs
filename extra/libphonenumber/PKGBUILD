# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make -j1

pkgname=libphonenumber
pkgver=7.7.0
pkgrel=2
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
arch=(i686 x86_64)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest git java-environment)
url="https://github.com/googlei18n/libphonenumber"
license=("Apache")
_commit=1ec4d341c3cd13dc2ae05e0b372e85edfb41ee80
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-//;s/-/+/g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$pkgname/cpp
  make -j1
}

check() {
  cd build
  make tests
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
