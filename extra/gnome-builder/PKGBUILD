# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix build on ARM

pkgname=gnome-builder
pkgver=3.22.1+2+g4b26aa6
pkgrel=2
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(i686 x86_64)
license=(GPL3)
depends=(gtksourceview3 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils
         ctags libpeas vte3 vala python-jedi autoconf-archive)
makedepends=(intltool llvm gobject-introspection gtk-doc yelp-tools appstream-glib vala git
             mm-common)
groups=(gnome-extra)
_commit=4b26aa6aeb23b17c3d43364040fd1332aae4f0e1  # master
source=("git://git.gnome.org/gnome-builder#commit=$_commit"
        '0001-ARM-fix.patch')
sha256sums=('SKIP'
            'a92b25439576fe44565d3d7517be5f22d386806916fbc4abd1b3b304ed7b7ca2')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply ../0001-ARM-fix.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
