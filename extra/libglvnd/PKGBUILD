# $Id: PKGBUILD 249779 2015-10-27 00:47:25Z eric $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on !armv7h

pkgname=libglvnd
pkgver=0.1.1.20161028
_commit=470fc824a38521a52707c6c0f59d827aa5e0f45a
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('i686' 'x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
depends=('libxext')
makedepends=('libx11' 'glproto' 'git' 'python')
#source=("git+https://github.com/NVIDIA/libglvnd.git#tag=v$pkgver"
source=("git+https://github.com/NVIDIA/libglvnd.git#commit=$_commit"
        LICENSE
        https://patch-diff.githubusercontent.com/raw/NVIDIA/libglvnd/pull/110.patch
        https://patch-diff.githubusercontent.com/raw/NVIDIA/libglvnd/pull/97.patch
        0001-remove-unused-variable.patch)
sha1sums=('SKIP'
          'c88e07f333fcf8dcdbef370b85372b4d88ad8a66'
          '048e89dbdd0a6ca33441a6273208e4126d9828fc'
          '48fac1b1248f1a6766ebae66432269c5147b5815'
          '39eb9a4d3cb9484e9fa27c234c783e83c2569103')

prepare() {
  cd $pkgname
  # FS#51527
  patch -p1 -i ../97.patch
  patch -p1 -i ../110.patch
  patch -p1 -i ../0001-remove-unused-variable.patch
}

build() {
  cd $pkgname
  ./autogen.sh

  [[ $CARCH != "armv7h" ]] && CONFIG="--disable-asm"

  ./configure --prefix=/usr $CONFIG
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install

  # For compatibility with older nvidia drivers for bumblebee
  # Symlinks to /usr/lib will be in nvidia-libgl
  mkdir "$pkgdir/usr/lib/nvidia"
  for _lib in libGL.so libGLESv1_CM.so libGLESv2.so libEGL.so; do
    mv "$pkgdir"/usr/lib/$_lib* "$pkgdir"/usr/lib/nvidia/
  done

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
