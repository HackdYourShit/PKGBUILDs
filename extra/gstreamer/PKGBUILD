# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich
#  - remove valgrind makedepend

pkgname=gstreamer
pkgver=1.10.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(LGPL)
depends=(libxml2 glib2 libunwind libcap)
makedepends=(intltool pkgconfig gtk-doc gobject-introspection autoconf-archive git)
_commit=100671b42db3cb4571e296d5cdf567ae62d75b04  # tags/1.10.1^0
install=gstreamer.install
source=("git://anongit.freedesktop.org/gstreamer/gstreamer#commit=$_commit"
        "gst-common::git://anongit.freedesktop.org/gstreamer/common")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
